100 ' NAME:  FROGS***
105 '
110 ' BY:  MAC OGLESBY ON 12/01/74
115 '
120 ' DESCRIPTION:  PROVIDES A REVERSING GAME IN WHICH THE PLAYER
125 '   STARTS WITH TWO SETS OF PIECES ON A BOARD NINE SQUARES LONG.
130 '   THE OBJECT IS TO REVERSE THE TWO SETS OF PIECES IN THE LEAST
135 '   NUMBER OF MOVES.
140 '
145 ' INSTRUCTIONS:  TYPE "RUN" FOR COMPLETE INSTRUCTIONS.
150 '
155 ' CATEGORY:  GAMES***
160 '
165 ' LANGUAGE:  BASIC
170 '
175 ' INDEX LINE:
180 '     A !REVERSING !GAME WITH TWO SETS OF !PIECES
190 '
195 '
1000 LET T=5
1010 LET E=0
1020 FOR J=1 TO 4           'SET UP PLAYING BOARD
1030    LET P(J)=ASC(*)
1040 NEXT J
1050 LET P(5)=ASC( )
1060 FOR J=6 TO 9
1070    LET P(J)=ASC(%)
1080 NEXT J
1090 PRINT "WELCOME TO THE GAME OF FROGS!"
1100 PRINT
1110 PRINT "FOR INSTRUCTIONS, JUST PRESS 'RETURN'."
1120 PRINT
1130 
1140 'PRINT THE BOARD, NUMBERING SQUARES ONLY AT THE BEGINNING,
1150 'AFTER ERROR MESSAGES AND AFTER EVERY T MOVES.
1160 FOR J=1 TO 9
1170    PRINT " ";CHR$(P(J));
1180 NEXT J
1190 IF M<24 THEN 1350
1200    FOR J=1 TO 4                   'CHECK FOR WIN
1210       IF P(J)=ASC(%) THEN 1230
1220          GOTO 1350
1230    NEXT J
1240    FOR J=6 TO 9
1250       IF P(J)=ASC(*) THEN 1270
1260          GOTO 1350
1270    NEXT J
1280    PRINT
1290    IF M<>24 THEN 1310
1300       PRINT "*** CONGRATULATIONS ***"
1310       PRINT "YOU'VE REVERSED THE FROGS IN ";M;" MOVES"
1320       PRINT "TYPE RUN TO PLAY AGAIN"
1330       PRINT
1340       STOP
1350 IF INT(E/T)<>E/T THEN 1390        'SEE IF SQUARES SHOULD BE NUMBERED
1360    PRINT
1370    PRINT
1380    PRINT " 1 2 3 4 5 6 7 8 9";
1390 LET E=E+1
1400 PRINT "  ";
1410
1420 LINPUT C$                         'GET USER'S MOVE
1430 IF LEN(C$)=0 THEN 1710
1440    IF LEN(C$)<>2 THEN 1650
1450       CHANGE C$ TO C
1460       IF (57-C(1))*(C(1)-49)<0 THEN 1650  'LOOK FOR A DIGIT FROM 1-9
1470          LET C(1)=C(1)-48
1480          IF (57-C(2))*(C(2)-49)<0 THEN 1650
1490             LET C(2)=C(2)-48
1500             'SEE IF LEGAL MOVE
1510             IF P(C(1))=32 THEN 1630
1520                IF P(C(2))<>32 THEN 1610
1530                   IF ABS(C(1)-C(2))>2 THEN 1590
1540                      'TALLY AND IMPLEMENT MOVES
1550                      LET M=M+1
1560                      LET P(C(2))=P(C(1))
1570                      LET P(C(1))=ASC( )
1580                      GOTO 1160    'CONTINUE THE GAME
1590                   PRINT "THAT'S TOO BIG A JUMP!"
1600                   GOTO 1670
1610                PRINT "SORRY, BUT SQUARE ";C(2);" IS OCCUPIED!"
1620                GOTO 1670
1630             PRINT "YOU CAN'T MOVE AN EMPTY SPACE!"
1640             GOTO 1670
1650     PRINT "TO MOVE A PIECE, TYPE 2 DIGITS (BUT NOT ZERO)."
1660     PRINT "FOR INSTRUCTIONS, JUST PRESS 'RETURN'."
1670     PRINT "INPUT IGNORED. PLEASE TRY AGAIN..."
1680     LET E=8
1690     GOTO 1160
1700 'INSTRUCTIONS
1710 PRINT
1720 PRINT "YOU START WITH 8 PIECES ON 9 SQUARES (NUMBERED 1 THRU 9)."
1730 PRINT "THE OBJECT IS TO REVERSE THE TWO SETS OF PIECES IN THE FEWEST"
1740 PRINT "POSSIBLE MOVES (24). THE STARS (*) MUST END UP IN SQUARES"
1750 PRINT "6 TO 9 AND THE PERCENT SIGNS (X) IN SQUARES 1 TO 4."
1760 PRINT
1770 PRINT "AT EACH MOVE YOU MAY SLIDE OR JUMP ONE PIECE. A SLIDE IS A"
1780 PRINT "MOVE TO AN ADJACENT EMPTY SQUARE. A JUMP MEANS TO HOP OVER"
1790 PRINT "AN ADJACENT PIECE TO THE EMPTY SQUARE JUST BEYOND IT."
1800 PRINT
1810 PRINT "FOR YOUR MOVE YOU MUST TYPE 2 NUMBERS. THE FIRST NAMES THE"
1820 PRINT "PIECE YOU WISH TO MOVE; THE SECOND, THE SQUARE TO GO TO."
1830 PRINT "FOR EXAMPLE, 57 MEANS THE PIECE AT SQUARE 5 JUMPS TO SQUARE 7."
1840 PRINT
1850 PRINT "TYPE YOUR MOVE AFTER THE QUESTION MARK."
1860 GOTO 1680
1870
1880 END
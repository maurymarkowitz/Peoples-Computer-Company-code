100 ' NAME: DODGEM***
105
110 ' BY: MAC OGLESBY ON 10/18/75
120
125 ' DESCRIPTION:  TWO SETS OF PIECES RACE AT RIGHT ANGLES ACROSS A
125 '   SQUARE BOARD.  FOR ONE OR TWO PLAYERS.
130 '
135 ' INSTRUCTIONS:  TYPE "RUN" FOR COMPLETE INSTRUCTIONS.
140 '
145 ' REMARKS:  THE GAME OF DODGEM IS DESCRIBED BY MARTIN GARDNER IN
150 '   'SCIENTIFIC AMERICAN', JUNE 1975.
155 '
160 ' CATEGORY:  KIDCAT***
165 '
170 ' LANGUAGE:  BASIC
175 '
180 ' INDEX LINE:
185 '     !ONE OR !TWO PLAYER !DODGEM !GAME
190 '
195 '
230 RANDOMIZE
240 PRINT "WANT INSTRUCTIONS FOR DODGEM";
250 LINPUT A$
260 GOSUB 2950                        'PROCESS INPUT
270 IF SEG$(A$,1,1)<>"Y" THEN 290
280    GOSUB 3130                     'INSTRUCTIONS
290 PRINT "BOARD SIZE (3-6)";
300 INPUT A
310 LET A=INT(A)
320 IF (6-A)*(A-3)=>0 THEN 340
330    GOTO 290
340 LET P(1,0)=P(2,0)=A-1             'HOW MANY PIECES
350
360
370 ' MATRIX P(,) KEEPS TRACK OF PIECES
380 FOR J=1 TO A-1
390    LET P(1,J)=10*J+1              'LOCATE DIGITS
400 NEXT J
410
420 FOR J=1 TO A-1
430    LET P(2,J)=10*A+J+1            'LOCATE LETTERS
440 NEXT J
450
460 LET F=1
470 LET M$(1)="NES"                   'LEGAL MOVES FOR THE DIGITS
480 LET M$(2)="NE W"                  'LEGAL MOVES FOR THE LETTERS
490 LET C$(1)="DIGITS"
500 LET C$(2)="LETTERS"
510 LET A$(1)="12345"
520 LET A$(2)="ABCDE"
530
540
550 ' SET UP BOARD
560 FOR J=1 TO A
570    IF J=A THEN 630
580       LET D$(J,1)=CHR$(48+J)
590       FOR K=2 TO A
600          LET D$(J,K)="."
610       NEXT K
620       GOTO 670
630    LET D$(J,1)="."
640    FOR K=2 TO A
650       LET D$(J,K)=CHR$(63+K)
660    NEXT K
670 NEXT J
680
690 PRINT "HOW MANY PLAYERS (1 OR 2)";
700 INPUT B
710 IF B=2 THEN 800
720    IF B=1 THEN 740
730       GOTO 690
740    PRINT "OK, THE COMPUTER WILL MOVE THE DIGITS."
750    PRINT "WHO MOVES FIRST (1=COMPUTER 2=YOU)";
760    INPUT F
770    IF (2-F)*(1-F)=0 THEN 800
780       PRINT "PLEASE TYPE 1 OR 2. NOW, ";
790       GOTO 750
800 PRINT
810 PRINT "HERE WE GO..."
820
830 ' PRINT THE DISPLAY
840 PRINT
850 FOR J=1 TO A
860    FOR K=1 TO A
870       PRINT " ";D$(J,K);
880    NEXT K
890    PRINT
900 NEXT J
910 PRINT
920
930
940 ' MAIN MOVE LOOP
950 FOR J=F TO 3-F STEP 3-2*F
960
970    ' CHECK IF PLAYER J HAS A LEGAL MOVE LEFT
980    FOR J1=1 TO A-1
990       LET R=INT(P(J,J1)/10)       'ROW OF J1TH PIECE
1000       LET C=P(J,J1)-10*R          'COLUMN OF J1TH PIECE
1010
1020       ON J GOTO 1040,1100
1030
1040       IF C=A THEN 1080            'AT EDGE OF BOARD?
1050          IF C>A THEN 1070         'OFF BOARD?
1060             GOTO 1160             'SEE IF PIECE CAN MOVE
1070          GOTO 1250                'TRY NEXT PIECE
1080       GOTO 1300                   'LEGAL MOVE EXISTS
1090
1100       IF R=1 THEN 1140            'EDGE OF BOARD?
1110          IF R=0 THEN 1130         'OFF BOARD?
1120             GOTO 1160             'SEE IF PIECE CAN MOVE
1130          GOTO 1250                'TRY NEXT PIECE
1140       GOTO 1300                   'LEGAL MOVE EXISTS
1150
1160       IF D$(R-1,C)="." THEN 1240  'SEE IF PIECE CAN GO NORTH
1170          IF D$(R,C+1)="." THEN 1240  'SEE IF PIECE CAN GO EAST
1180             IF J=2 THEN 1220
1190                IF D$(R+1,C)="." THEN 1210  'CAN IT GO SOUTH
1200                   GOTO 1250       'TRY NEXT PIECE
1210                GOTO 1300          'LEGAL MOVE EXISTS
1220             IF D$(R,C-1)="." THEN 1240  'SEE IF PIECE CAN GO WEST
1230                GOTO 1250          'TRY NEXT PIECE
1240       GOTO 1300                   'LEGAL MOVE EXISTS
1250    NEXT J1
1260    PRINT "THE ";C$(3-J);" HAVE LEFT NO LEGAL MOVE FOR THE ";C$(J);"!"
1270    PRINT "*** THE ";C$(J);" WIN!!!"
1280    STOP
1290
1300    IF B=2 THEN 1750
1310       IF J=2 THEN 1750
1320
1330          ' GENERATE COMPUTER'S MOVE
1340          LET L1=2
1350          FOR L0=1 TO 3
1360
1370             ON L0 GOTO 1450,1380,1430
1380             IF RND<.5 THEN 1410
1390             LET L1=1
1400             GOTO 1450
1410             LET L1=3
1420             GOTO 1450
1430             LET L1=4-L1
1440
1450             LET P1=INT(RND*A)
1460             FOR L2=1 TO A-1
1470             LET P1=P1+1
1480             IF P1<=A-1 THEN 1500
1490                LET P1=P1-(A-1)
1500             LET R=INT(P(J,P1)/10)  'ROW
1510             LET C=P(J,P1)-10*R  'COLUMN
1520             IF C>A THEN 1720
1530
1550                ON L1 GOTO 1570,1620,1690
1550
1560                ' NORTH
1570                IF D$(R-1,C)="." THEN 1590
1580                   GOTO 1720
1590                GOTO 2090
1600
1610                ' EAST
1620                IF D$(R,C+1)="." THEN 1660
1630                   IF C=A THEN 1650
1640                      GOTO 1720
1650                   LET P(J,0)=P(J,0)-1
1660                GOTO 2160
1670
1680                'SOUTH
1690                IF D$(R+1,C)="." THEN 1710
1700                   GOTO 1720
1710                GOTO 2230
1720          NEXT L2
1730       NEXT L0
1740
1750    PRINT C$(J);" MOVE";
1760    LINPUT A$
1770    GOSUB 2950                      'PROCESS INPUT
1780
1790    IF SEG$(A$,1,1)="R" THEN 2860
1800       IF SEG$(A$,1,1)="H" THEN 2460
1810          LET A$=SEG$(A$,1,2)      'LOOK ONLY AT 1ST 2 CHARACTERS
1820          LET P1=POS(SEG$(A$(J),1,A-1),SEG$(A$,1,1)+1)  'WHAT PIECE
1830          IF P1=0 THEN 2430
1840             LET P2=POS(M$(J),SEG$(A$,2,2),1)  'WHAT DIRECTION
1850             IF P2=0 THEN 2430
1860                LET R=INT(P(J,P1)/10)  'ROW OF PIECE
1870                LET C=P(J,P1)-10*R  'COLUMN OF PIECE
1880                IF R=0 THEN 2430
1890                   IF C>A THEN 2430
1900
1910                      ON J GOTO 1930,1990
1920
1930                      IF C<A THEN 1970  'PIECE NOT AT EAST EDGE
1940                         IF P2<>2 THEN 1970  'DIRECTION NOT EAST
1950                            LET P(1,0)=P(1,0)-1  'REDUCE # OF PIECES
1960                            GOTO 2160  'UPDATE PIECE LOCATION
1970                      GOTO 2840    'CHECK MOVE FURTHER
1980
1990                      IF R>1 THEN 2040  'PIECE NOT AT NORTH EDGE
2000                         IF P2<>1 THEN 2040  'DIRECTION NOT NORTH
2010                            LET P(2,0)=P(2,0)-1  'REDUCE # OF PIECES
2020                            GOTO 2090  'UPDATE LOCATIONS
2030
2040                      ON P2 GOTO 2070,2140,2210,2280
2050
2060                      ' SEE IF PIECE CAN MOVE NORTH
2070                      IF D$(R-1,C)="." THEN 2090
2080                         GOTO 2430  'ERROR MESSAGE
2090                      LET D$(R-1,C)=SEG$(A$(J),P1,P1)
2100                      LET P(J,P1)=P(J,P1)-10
2110                      GOTO 2330
2120
2130                      ' EAST
2140                      IF D$(R,C+1)="." THEN 2160
2150                         GOTO 2430  'ERROR MESSAGE
2160                      LET D$(R,C+1)=SEG$(A$(J),P1,P1)
2170                      LET P(J,P1)=P(J,P1)+1
2180                      GOTO 2330
2190
2200                      ' SOUTH
2210                      IF D$(R+1,C)="." THEN 2230
2220                         GOTO 2430  'ERROR MESSAGE
2230                      LET D$(R+1,C)=SEG$(A$(J),P1,P1)
2240                      LET P(J,P1)=P(J,P1)+10
2250                      GOTO 2330
2260
2270                      ' WEST
2280                      IF D$(R,C-1)="." THEN 2300
2290                         GOTO 2430  'ERROR MESSAGE
2300                      LET D$(R,C-1)=SEG$(A$(J),P1,P1)
2310                      LET P(J,P1)=P(J,P1)-1
2320
2330                      LET DS(R,C)="."  'FINISH UPDATING DISPLAY
2340                      IF B=2 THEN 2380
2350                         IF J=2 THEN 2350
2360                            PRINT "THE DIGITS MOVE:  ";SEG$(A$(J),P1,P1);SEG$(M$),L1,L1)
2370                            'SEG$(AS(J),P1,P1);SEG$(M$(J),L1,L1)
2380                      IF P(J,0)<>0 THEN 2420  'CHECK FOR WIN
2390                         PRINT
2400                         PRINT "*** THE ";C$(J);" WIN!!!"
2410                         STOP
2420                      GOTO 2900  'GET NEXT PLAYER'S MOVE
2430          PRINT "ILLEGAL MOVE OR BAD INPUT."
2440          PRINT "INPUT IGNORED. TYPE H FOR HELP."
2450          GOTO 1750
2460       PRINT "THE ";C$(J);" HAVE THESE LEGAL MOVES:"
2470       ' HELP! -- PRINT LEGAL MOVES
2480       FOR J3=1 TO A-1
2490          LET P$=SEG$(A$(J),J3,J3)  'NAME OF PIECE
2500          LET R=INT(P(J,J3)/10)    'ROW
2510          LET C=P(J,J3)-10*R       'COLUMN
2520
2530          ON J GOTO 2550,2620
2540
2550          IF C=A THEN 2590         'PIECE AT EAST EDGE
2560             IF C>A THEN 2580      'OFF BOARD
2570                GOTO 2690          'CHECK FURTHER
2580             GOTO 2830             'TRY NEXT PIECE
2590          PRINT "  ";P$;"E";
2600          GOTO 2690
2610
2620          IF R=1 THEN 2660         'NORTH EDGE
2630             IF R=0 THEN 2650      'OFF BOARD
2640                GOTO 2690
2650             GOTO 2830
2660          GOTO 2700
2670
2680          ' NORTH
2690          IF D$(R-1,C)<>"." THEN 2730
2700             PRINT "  ";P$;"N";
2710
2720             ' EAST
2730          IF DS(R,C+1)<>"." THEN 2770
2740             PRINT "  ";P$;"E";
2750
2760             ' SOUTH AND WEST
2770          IF J=2 THEN 2810
2780             IF D$(R+1,C)<>"." THEN 2800
2790                PRINT "  ";P$;"S";
2800             GOTO 2830
2810          IF D$(R,C-1)<>"." THEN 2830
2820             PRINT "  ";P$;"W";
2830       NEXT J3
2840       PRINT
2850       GOTO 1750                   'TRY AGAIN
2860    PRINT "THE ";C$(J);" GIVE UP!!"
2870    PRINT "*** THE ";C$(3-J);" WIN!!!"
2880     STOP
2890
2900 NEXT J
2910 GOTO 840                          'PRINT DISPLAY
2920
2930
2940 ' PROCESS INPUT: CHANGE LOWERCASE TO UPPERCASE, IGNORE COMMAS, SPACES
2950 IF LEN(A$)>10 THEN 3090
2960    LET C1=0                       'RESET COUNTER
2970    CHANGE A$ TO A
2980    FOR J2=1 TO A(0)
2990       IF A(J2)<96 THEN 3010       'CHECK FOR UPPERCASE
3000          LET A(J2)=A(J2)-32       'CHANGE TO UPPERCASE
3010       IF (57-A(J2))*(A(J2)-48)=>0 THEN 3040  'CHECK FOR A DIGIT
3020          IF (90-A(J2))*(A(J2)-65)=>0 THEN 3040  'CHECK FOR A LETTER
3030             GOTO 3060             'ELSE IGNORE THIS CHARACTER
3040       LET C1=C1+1
3050       LET A(C1)=A(J2)             'STORE THIS CHARACTER
3060    NEXT J2
3070    LET A(0)=C1
3080    CHANGE A TO A$
3090 RETURN
3100
3110
3120 ' INSTRUCTIONS
3130 PRINT
3140 PRINT "HERE'S A SAMPLE PLAYING BOARD:"
3150 PRINT
3160 PRINT "1 . . . ."
3170 PRINT "2 . . . ."
3180 PRINT "3 . . . ."
3190 PRINT "4 . . . ."
3200 PRINT ". A B C D"
3210 PRINT
3220 PRINT "TWO SETS OF PIECES (DIGITS AND LETTERS) RACE AT RIGHT ANGLES"
3230 PRINT "ACROSS A SQUARE BOARD.  VACANT LOCATIONS ARE SHOWN AS PERIODS."
3240 PRINT "YOU GET TO CHOOSE THE BOARD SIZE.  (THE ONE ABOVE IS SIZE 5.)"
3250 PRINT
3260 PRINT "  N"
3270 PRINT "  :"
3280 PRINT "W---E"
3290 PRINT "  :"
3300 PRINT "  S"
3310 PRINT
3320 PRINT "THE OBJECT IS TO MOVE ALL OF YOUR PIECES ACROSS THE BOARD"
3330 PRINT "AND OFF THE OPPOSITE EDGE.  DIGITS LEAVE THE BOARD ONLY AT"
3340 PRINT "THE EASTERN EDGE; LETTERS ONLY AT THE NORTHERN.  THE WINNER"
3350 PRINT "IS THE PLAYER WHOSE PIECES HAVE ALL LEFT THE BOARD."
3360 PRINT
3370 PRINT "THE PLAYERS GO IN TURN, MOVING ONE OF THEIR PIECES TO AN"
3380 PRINT "ADJACENT LOCATION WHICH IS EITHER OFF THE BOARD OR CURRENTLY"
3390 PRINT "VACANT.  THERE ARE NO DIAGONAL MOVES, NO JUMPS AND NO CAPTURES"
3400 PRINT "DIGITS CANNOT MOVE WEST, NOR LETTERS MOVE SOUTH."
3410 PRINT
3420 PRINT "TO MOVE A PIECE, TYPE ITS NAME AND THE FIRST LETTER OF THE"
3430 PRINT "DESIRED DIRECTION.  EXAMPLES:"
3440 PRINT "  2E  MEANS PIECE  2  WANTS TO GO EAST"
3450 PRINT "  BW  MEANS PIECE  B  WANTS TO GO WEST."
3460 PRINT
3470 PRINT "NOTE:  YOU FORFEIT THE GAME IF YOUR MOVE LEAVES YOUR OPPONENT"
3480 PRINT "WITHOUT ANY LEGAL MOVE."
3490 PRINT
3500 PRINT "LASTLY, YOU MAY TYPE R TO RESIGN OR H FOR HELP."
3510 PRINT
3520 RETURN
3530 END
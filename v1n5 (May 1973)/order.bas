10 DIM A[100,2]
100 REM *** TALK TO HUMAN ***
110 PRINT "HELLO, HOW MANY VALUES ";
120 INPUT N
130 PRINT
140 A[N+1,1]=0
150 A[N+1,2]=0
200 REM *** GENERATE RANDOM VALUES ***
210 FOR I=1 TO N
220 A[I,1]=RND(0)
230 J=N+1
300 REM *** LOOP TO FIND PLACE TO INSERT ***
310 K=A[J,2]
320 IF K=0 THEN 400
330 IF A[K,1]>A[I,1] THEN 400
340 J=K
350 GOTO 300
400 REM *** INSERT IN LIST ***
410 A[I,2]=K
420 A[J,2]=I
430 NEXT I
500 REM *** PRINT THE ARRAY ***
510 PRINT "ORIGINAL ARRAY :"
420 PRINT
530 PRINT "ADDRESS","VALUE","POINTER"
540 PRINT
550 FOR I=1 TO N
560 PRINT I,A[I,1],A[I,2]
570 NEXT I
580 PRINT
600 REM *** PRINT THE LIST ***
610 PRINT
620 PRINT "SORTED LIST :"
630 PRINT
640 PRINT "ADDRESS","VALUE","POINTER"
650 PRINT
660 J=N+1
700 REM *** LOOP TO PRINT LIST ***
710 K=A[J,2]
720 IF K=0 THEN 800
730 PRINT K,A[K,1],A[K,2]
740 J=K
750 GOTO 700
800 REM *** SAY BYE BYE ***
810 PRINT
820 PRINT "BYE BYE HUMAN !!!"
830 PRINT
840 END
100 REM *** SNARK *** CATCH HIM WITH A WELL PLACED CIRCLE
110 REM *** PEOPLE'S COMPUTER COMPANY
120 PRINT "WANT THE RULES";
130 INPUT Z$[1,1]
140 IF Z$ <> "Y" THEN 390
150 REM *** HERE ARE THE RULES
160 PRINT
170 PRINT "A SNARK IS HIDING IN A 10 BY 10 GRID LIKE THE ONE"
180 PRINT "SHOWN BELOW:"
190 PRINT
200 PRINT " Y:
210 FOR Y=9 TO 0 STEP -1
220 PRINT Y;" . . . . . . . . . ."
230 NEXT Y
240 PRINT
250 PRINT TAB(6);" 0 1 2 3 4 5 6 7 8 9 X"
260 PRINT
270 PRINT "TRY TO CATCH HIM. HERE'S HOW ... WHEN I ASK, YOU TYPE"
280 PRINT "THE X,Y COORDINATES OF A GRIDPOINT (IF YOU DON'T KNOW"
290 PRINT "WHAT THAT MEANS, ASK SOMEONE!) AND PRESS THE RETURN"
300 PRINT "KEY. THEN, WHEN I ASK FOR THE 'RADIUS', YOU TYPE THE RADIUS"
310 PRINT "OF A CIRCLE CENTERED ON THE GRIDPOINT WHOSE X,Y"
320 PRINT "COORDINATES YOU JUST ENTERED. I WILL THEN TELL YOU"
330 PRINT "WHETHER THE SNARK IS 'INSIDE' YOUR CIRCLE, 'OUTSIDE'"
340 PRINT "YOUR CIRCLE, ON 'ON' YOUR CIRCLE."
350 PRINT
360 PRINT "!!! IMPORTANT !!! IF YOU THINK YOU KNOW WHERE HE IS"
370 PRINT "HIDING, CENTER 0 (ZERO) AS THE RADIUS. GOOD HUNTING."
380 REM *** HIDE THE SNARK
390 LET X=INT(10*RND(0))
400 LET Y=INT(10*RND(0))
410 PRINT
420 PRINT "SNARK IS HIDING ... START GUESSING!"
430 REM *** GUESSING BEGINS
440 K=1
450 PRINT
460 PRINT "COORDINATES":
470 INPUT A,B
480 IF A=INT(A) AND B=INT(B) THEN 510
490 PRINT "FORGOT TO TELL YOU - COORDINATES MUST BE INTEGERS!"
500 GOTO 450
510 LET D2=(X-A)*(X-A)+(Y-B)*(Y-B)
520 PRINT "RADIUS";
530 INPUT R
540 IF R=INT(R) AND R >= 0 THEN 580
550 PRINT "WHOOPS! THE RADIUS MUST BE A WHOLE NUMBER."
560 PRINT
570 GOTO 520
580 IF R <> 0 THEN 600
590 IF D2=0 THEN 710
600 IF D2<R*R THEN 630
610 IF D2>R*R THEN 650
620 IF D2=R*R THEN 670
630 PRINT "SNARK IS INSIDE YOUR CIRCLE"
640 GOTO 680
650 PRINT "SNARK IS OUTSIDE YOUR CIRCLE"
660 GOTO 680
670 PRINT "SNARK IS ON YOUR CIRCLE"
680 K=K+1
690 GOTO 450
700 REM *** WE GOT A WINNER
710 PRINT
720 PRINT "YOU CAUGHT HIM IN";K;"GUESSES!!!"
730 PRINT "GOOD SHOW!"
740 PRINT
750 PRINT "WANT TO PLAY AGAIN";
760 INPUT Z$[1,1]
770 IF Z$="Y" THEN 390
780 END
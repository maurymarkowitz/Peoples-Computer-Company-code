100 ' NAME: ELEMLIB***FACTORS
110
120 ' BY: MAC OGLESBY ON 5 AUG 75.
130
140 ' DESCRIPTION: GIVEN WHOLE NUMBERS P AND Q. THIS PROGRAM PRINTS
150 ' ALL OF THE WHOLE NUMBER FACTORS FOR EACH WHOLE NUMBER FROM P
160 ' TO Q. THE USER SUPPLIES P AND Q, WHICH MUST BE WITHIN THE RANGE
170 ' 1 TO 10000. THE LISTS GENERATED MAY BE USED AS MATHLAB OR CLASS-
180 ' ROOM REFERENCE CHARTS. A BLOCK OF 120 NUMBERS MAKES A GOOD
190 ' SIZE WALL CHART.
200
210 ' INSTRUCTIONS: WHEN ASKED, SUPPLY 2 WHOLE NUMBERS (THEY MAY BE
220 ' THE SAME NUMBER) FROM 1 TO 10000.
230
240
250
260 PRINT "PLEASE LIST THIS PROGRAM FOR COMPLETE INSTRUCTIONS."
270
280 DIM F(32)                         'SAVE SPACE FOR 32 STORED FACTORS
290
300 ' GET P AND Q AND CHECK LEGALITY
310 PRINT
320 PRINT "TYPE TWO WHOLE NUMBERS SEPARATED BY A COMMA";
330 INPUT P,Q
340 IF (10000-P)*(P-1)<0 THEN 390
350    IF P<>INT(P) THEN 390
360       IF (10000-Q)*(Q-1)<0 THEN 390
370           IF Q<>INT(Q) THEN 390
380              GOTO 410
390 PRINT "PLEASE PICK WHOLE NUMBERS FROM 1 TO 10000."
400 GOTO 320
410 IF Q=>P THEN 450                  'CHECK IF Q=>P
420    LET P9=P                       'ELSE REVERSE P AND Q
430    LET P=Q
440    LET Q=P9
450 PRINT
460 PRINT
470 PRINT "NUMBER   FACTORS"
480 PRINT "------   -------"
490
500 ' COMPUTE AND PRINT THE FACTORS
510 FOR J=P TO Q
520    LET F=0                        'RESET STONED FACTOR COUNTER
530    LET M=0                        'RESET CHARACTER COUNTER
540    LET R=SQR(J)
550    PRINT
560    PRINT J;TAB(8);                'PRINT THE NUMBER, SPACES
570    FOR K=1 TO R
580       LET X=J/K
590       IF X<>INT(X) THEN 710       'SEE IF K IS A FACTOR OF J
600       LET G=LEN(STR$(K))+2        'G IS ROOM NEEDED BY FACTOR
610       IF M+G<=64 THEN 650         'STILL ROOM ON THIS LINE
620          PRINT
630          PRINT TAB(8);            'START A NEW LINE
640          LET M=0                  'RESET M
650       PRINT K;                    'PRINT FACTOR
660       LET M=M+G                   'ADD G TO M
670       IF X<>R THEN 690            'SEE IF FACTOR IS SQR OF J
680          GOTO 720                 'GO PRINT STORED FACTORS
690       LET F=F+1                   'TALLY STORED FACTORS
700       LET F(F)=X                  'F(F) STORES FACTOR J/K
710    NEXT K
720    FOR K=F TO 1 STEP -1           'PRINT THE STORED FACTORS
730       LET G=LEN(STR$(F(K)))+2
740       IF M+G<=64 THEN 780
750          PRINT
760          PRINT TAB(8);
770          LET M=0
780       PRINT F(K);                 'PRINT FACTOR F(K)
790       LET M=M+G
800    NEXT K
810    PRINT
820 NEXT J
830
840 PRINT
850 PRINT
860 PRINT
870 PRINT "TO STOP, TYPE THE LETTER 'S'.  TO CONTINUE."
880 GOTO 320
890 END
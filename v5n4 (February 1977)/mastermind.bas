1000 REM        M A S T E R M I N D  --  A   G A M E   O F   L O G I C
1010 REM
1020 REM        WRITTEN BY: JESSE HEINES AND GAY ROSSER SEPTEMBER 1976
1030 REM
1040 REM
1050 REM        ADAPTED BY PERMISSION FROM THE ORIGINAL GAME COPYRIGHTED
1060 REM        BY INVICTA PLASTICS (USA), LIMITED, NEW YORK, N.Y. 10010
1070 REM
1080 REM
1090 REM
1100 REM  ****  VARIABLE DIRECTORY
1110 REM
1120 REM        A$      ALPHANUMERIC PLAYER INPUT
1130 REM        C$(X)   SECRET CODE
1140 REM        F0(X)   GUESS FLAGS  (1 = MATCH ON THIS CODE PEG)
1150 REM        F1(X)   CODE FLAGS   (1 = EXACT OR COLOR MATCH WITH THIS
1160 REM                  GUESS PEG)
1170 REM        G$(X)   PLAYER'S GUESSES
1180 REM        KO,K1.  LOOP INDICES
1190 REM        LO      LINE COUNTER
1200 REM        P$(X)   COLORS OF PEGS
1210 REM        S0      EXACT MATCH COUNTER -- "BLACK KEYS"
1220 REM        S1      COLOR MATCH COUNTER -- "WHITE KEYS"
1230 REM        TO      TOTAL NUMBER OF LINES GUESSED
1240 REM        T1      TOTAL NUMBER OF GAMES PLAYED
1250 REM        T2      TOTAL NUMBER OF GAMES WON
1260 REM        X       CODE NUMBER OF RANDOMLY SELECTED PEG
1270 REM
1280 REM  ****  FUNCTION DIRECTORY
1290 REM
1300 REM        LEFT$(X$,Y)     RETURNS THE FIRST THROUGH YTH CHARACTERS
1310 REM                          OF X$
1320 REM        RND(0)          RETURNS A RANDOM NUMBER BETWEEN 0 AND 1
1330 REM
1340 REM  ****  DIMENSION ARRAYS AND INITIALIZE VARIABLES
1350 REM
1360    DIM C$(8), G$(4), P$(12), F0(4), F1(4)
1370    FOR K0=1 TO 12
1380       READ P$(K0)
1390    NEXT K0
1400    DATA RD,YW,GN,BU,WH,BK,RED,YELLOW,GREEN,BLUE,WHITE,BLACK
1410    RANDOMIZE
1420    T0=0
1430    T1=0
1440    T2=0
1450 REM
1460 REM  ****  PRINT TITLE AND ASK ABOUT DIRECTIONS
1470 REM
1480    PRINT "M A S T E R M I N D"
1490    PRINT
1500    PRINT
1510    PRINT "WOULD YOU LIKE TO SEE THE INSTRUCTIONS";
1520    INPUT A$
1530    IF LEFT$(A$,1)="Y" THEN 1600
1540    IF LEFT$(A$,1)="N" THEN 2000
1550      PRINT "   PLEASE RESPOND WITH 'YES' OR 'NO'.  YOUR CHOICE";
1560      GOTO 1520
1570 REM
1580 REM  ****  PRINT INSTRUCTIONS
1590 REM
1600    PRINT
1610    PRINT
1620    PRINT
1630    PRINT "     THE GOAL OF MASTERMIND IS TO FIGURE OUT THE COMPUT";
1640    PRINT "ER'S SECRET  CODE"
1650    PRINT "WITH  AS  FEW GUESSES AS POSSIBLE.  THE CODE IS MADE UP";
1660    PRINT " OF 4 PEGS.  EACH"
1670    PRINT "PEG MAY BE ANY OF 6 COLORS:  RED, YELLOW, GREEN, BLUE, ";
1680    PRINT "BLACK, OR  WHITE."
1690    PRINT "A SINGLE COLOR MAY BE USED 2 OR EVEN MORE TIMES IN THE ";
1700    PRINT "SAME CODE."
1710    PRINT
1720    PRINT "     YOU GUESS THE CODE BY TYPING 4 COLORS IN SEQUENCE.";
1730    PRINT "   THE COLORS MUST"
1740    PRINT "BE SEPARATED BY COMMAS, AND YOU MUST ALWAYS PRESS THE. ";
1750    PRINT "RETURN KEY WHEN"
1760    PRINT "YOU  FINISH  TYPING.  THE  COMPUTER  WILL  TELL YOU HOW";
1770    PRINT " YOU HAVE DONE BY"
1780    PRINT "INDICATING HOW MANY 'KEYS'  YOU HAVE SCORED.  BLACK KEY";
1790    PRINT "S  INDICATE  THAT"
1800    PRINT "YOU  HAVE  MATCHED  A  CODE PEG EXACTLY -- YOU HAVE A C";
1810    PRINT "ORRECT COLOR IN A"
1820    PRINT "CORRECT POSITION.  WHITE KEYS INDICATE THAT YOU HAVE A ";
1830    PRINT "CORRECT COLOR  IN"
1840    PRINT "AN INCORRECT POSITION."
1850    PRINT
1860    PRINT "     FOR EXAMPLE, IF THE SECRET CODE IS 'GREEN, YELLOW,";
1870    PRINT " GREEN, RED'  AND"
1880    PRINT "YOU  GUESS 'BLUE, GREEN, RED, RED', YOU WOULD SCORE 1 B";
1890    PRINT "LACK KEY FOR YOUR"
1900    PRINT "'RED'  GUESS  IN  POSITION  4  AND 1 WHITE KEY FOR YOUR";
1910    PRINT " 'GREEN' GUESS IN"
1920    PRINT "POSITION 2."
1930    PRINT
1940    PRINT "     THE  GAME  IS  OVER WHEN YOU HAVE FIGURED OUT THE ";
1950    PRINT "CODE AND SCORED 4"
1960    PRINT "BLACK KEYS.  IF YOU CANNOT FIGURE OUT THE CODE, TYPE  '";
1970    PRINT "UNCLE X,X,X'  AND"
1980    PRINT "THE COMPUTER WILL TELL YOU IT.  YOU CAN THEN START ANOT";
1990    PRINT "HER GAME."
2000    PRINT
2010    PRINT "     NOTE:  YOU  MAY  TYPE  THE COLOR NAMES IN FULL OR ";
2020    PRINT "USE THE FOLLOWING"
2030    PRINT "TWO-LETTER ABBREVIATIONS:"
2040    PRINT "   RD - RED              YW - YELLOW             GN - G";
2050    PRINT "REEN"
2060    PRINT "   BU - BLUE             WH - WHITE              BK - B";
2070    PRINT "LACK"
2080    PRINT
2090    IF LEFT$(A$,1)="N" THEN 2140
2100      PRINT "       GOOD LUCK!  PRESS THE 'CR' KEY WHEN YOU ARE READ";
2110      PRINT "Y TO PLAY. READY";
2120      INPUT A$
2130      PRINT
2140    PRINT
2150    PRINT
2160 REM
2170 REM  ****  SELECT SECRET CODE FOR THIS GAME & RESET ROUND COUNTER
2180 REM
2190    FOR K0=1 TO 4
2200       X=INT(6*RND(0))+1
2210       C$(K0)=P$(X)
2220       C$(K0+4)=P$(X+6)
2230    NEXT K0
2240    L0=0
2250    PRINT "G A M E  #" T1+1
2260    PRINT
2270 REM
2280 REM  ****  RESET KEY COUNTERS AND ALL FLAGS TO ZERO
2290 REM
2300    S0=0
2310    S1=0
2320    FOR K0=1 TO 4
2330       F0(K0)=0
2340       F1(K0)=0
2350    NEXT K0
2360 REM
2370 REM  ****  ACCEPT A GUESS
2380 REM
2390    L0=L0+1
2400    PRINT "LINE " LO "-- YOUR GUESS";
2410    INPUT G$(1), G$(2), G$(3), G$(4)
2420 REM
2430 REM  ****  CHECK FOR 'UNCLE'
2440 REM
2450    IF G$(1)<>"UNCLE" THEN 2540
2460      PRINT
2470      PRINT "   O.K., BETTER LUCK NEXT TIME."
2480      T0=T0-L0
2490      T2=T2-1
2500      GOTO 3020
2510 REM
2520 REM  **** CHECK FOR BAD GUESSES
2530 REM
2540    FOR K0=1 TO 4
2550       FOR K1=1 TO 12
2560         IF G$(K0)=P$(K1) THEN 2620
2570       NEXT K1
2580       PRINT "   YOUR '" G$(K0) "'x GUESS IN POSITION " K0 " IS NOT ";
2590       PRINT "VALID.  PLEASE TRY AGAIN."
2600       PRINT
2610       GOTO 2400
2620    NEXT KO
2630 REM
2640 REM  ****  CHECK FOR EXACT COLOR & POSITION MATCHES -- BLACK KEYS
2650 REM
2660    FOR K0=1 TO 4
2670       IF G$(K0)=C$(K0) THEN 2690
2680       IF G$(K0)<>C$(K0+4) THEN 2720
2690         F0(K0)=1
2700         F1(K0)=1
2710         S0=S0+1
2720     NEXT K0
2730 REM
2740 REM  ****  CHECK FOR COLOR MATCHES -- WHITE KEYS
2750 REM
2760    FOR K0=1 TO 4
2770      FOR K1=1 TO 4
2780        IF F0(K0)=1 THEN 2860
2790        IF F1(K1)=1 THEN 2860
2800        IF K0=K1 THEN 2860
2810        IF G$(K0)=C$(K1) THEN 2830
2820        IF G$(K0)<>C$(K1+4) THEN 2860
2830          F1(K1)=1
2840          S1=S1+1
2850          GOTO 2870
2860      NEXT K1
2870    NEXT KO
2880 REM
2890 REM  ****  CHECK FOR WIN (4 BLACK KEYS) & REPORT SCORE IF NO WIN
2900 REM
2910    IF S0=4 THEN 2980
2920      PRINT "   YOU GET " S0 " BLACK KEYS AND " S1 " WHITE KEYS."
2930      PRINT
2940      GOTO 2300
2950 REM
2960 REM  ****  CONFIRM WIN
2970 REM
2980    PRINT
2990    PRINT "   CONGRATULATIONS, YOU GET 4 BLACK KEYS! YOU FIGURED ";
3000    PRINT "OUT THE CODE IN "
3010    PRINT "  " L0 " LINES!"
3020    PRINT
3030    PRINT "   THE CORRECT SEQUENCE WAS:  ";
3040    PRINT C$(5) " - " C$(6) " - " C$(7) " - " C$(8)
3050    PRINT
3060    PRINT
3070    PRINT
3080 REM
3090 REM  ****  INCREMENT TOTAL COUNTERS AND ASK ABOUT ANOTHER GAME
3100 REM
3110    T0=T0+L0
3120    T1=T1+1
3130    T2=T2+1
3140    PRINT "WOULD YOU LIKE TO PLAY AGAIN";
3150    INPUT A$
3160    IF LEFT$(A$,1)="Y" THEN 2130
3170    IF LEFT$(A$,1)="N" THEN 3230
3180      PRINT "   PLEASE RESPOND WITH 'YES' OR 'NO'. YOUR CHOICE";
3190      GOTO 3150
3200 REM
3210 REM  ****  PRINT TOTAL SCORES AND STOP
3220 REM
3230    PRINT
3240    PRINT
3250    PRINT
3260    IF T2=0 THEN 3310
3270      PRINT "YOU FIGURED OUT THE SECRET CODE IN " T2 " OUT OF " T1;
3280      PRINT "GAMES WITH AN AVERAGE"
3290      PRINT "OF" T0/T2 "LINES PER GAME."
3300      PRINT
3310    PRINT "BYE!"
3320 END
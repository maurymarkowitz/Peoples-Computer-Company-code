100 ' NAME:  ELEMLIB***:SINNERS
110
120 ' BY:  MAC OGLESBY ON 02/27/76.
130
140 ' DESCRIPTION:  THREE OF SATAN'S FIENDS (MOVED BY THE COMPUTER)
150 '   PLAY AGAINST A GROUP OF CONDEMNED SINNERS (MOVED BY THE USER).
160
170 ' REMARKS:  THE GAME OF SINNERS IS BASED ON A GAME BY HAAR HOOLIM
180 '   DESCRIBED IN SID SACKSON'S BOOK, "A GAMUT OF GAMES" (RANDOM
190 '   HOUSE).
200 '
210 '
1000 DIM P(12,4)
1010 RANDOMIZE
1020 FOR J=1 TO 4
1030    READ M(J)                      'ASCII VALUES FOR N,E,S,W
1040 NEXT J
1050    DATA 78,69,83,87
1060 FOR J=1 TO 4
1070    READ R(J),C(J)                 'CHANGES NEEDED TO HOW COL TO GO
1080 NEXT J                            '   NORTH, EAST, SOUTH, WEST
1090    DATA -1,0,0,1,1,0,0,-1
1100
1110 'SET UP BOARD
1120 FOR J=0 TO 6
1130    FOR K=0 TO 6
1140       IF (5-J)*(J-1)<0 THEN 1180
1150          IF (5-K)*(K-1)<0 THEN 1180
1160             LET D$(J,K)="."       '25 DOTS IN A 5X5 ARRAY
1170             GOTO 1190
1180        LET D$(J,K)="@"            'SURROUND THE DOTS WITH @'S
1190    NEXT K
1200 NEXT J
1210 FOR I=1 TO 2                      'MAKE SURE FIENDS NOT IN LINE
1220    FOR J=1 TO 3
1230       LET H(I,J)=1+INT(RND*5)
1240       FOR K=1 TO J-1
1250          IF H(I,J)<>H(I,K) THEN 1270
1260             GOTO 1230
1270       NEXT K
1280    NEXT J
1290 NEXT I
1300 FOR J=1 TO 3
1310    LET D$(H(1,J),H(2,J))="*"      'PUT FIENDS ON THE BOARD
1320 NEXT J
1330
1340 PRINT "WANT INSTRUCTIONS FOR SINNERS";
1350 LINPUT A$
1360 GOSUB 2610                         'PROCESS INPUT
1370 IF A(1)<>89 THEN 1400
1380    GOSUB 2930
1390    GOTO 1420
1400 PRINT "HERE'S THE INITIAL SETUP:"
1410 GOSUB 2750
1420 PRINT "THE FIENDS MOVE FIRST."
1430
1440 'ROUTINES TO GENERATE COMPUTER'S MOVE
1450
1460 LET TI=0                          'RESET POSSIBLE MOVE COUNTER
1470 FOR J=1 TO 5                      'LOCATE POSSIBLE MOVES
1480    FOR K=1 TO 5
1490       IF D$(J,K)<>"*" THEN 1580
1500          FOR L=1 TO 4
1510             IF D$(J+R(L),K+C(L))<>"." THEN 1570
1520                LET TI=TI+1        'COUNT POSSIBLE MOVES
1530                LET P(TI,1)=J      'STORE MOVES IN MATRIX P(,)
1540                LET P(TI,2)=K
1550                LET P(TI,3)=J+R(L)
1560                LET P(TI,4)=K+C(L)
1570          NEXT L
1580    NEXT K
1590 NEXT J
1600 'WINNER?
1610 IF TI>0 THEN 1660
1620    PRINT "*** THE SINNERS HAVE LOST!!"  'BELLS
1630    GOSUB 2750
1640    PRINT "*** ABANDON HOPE, YE SINNERS!"  'BELLS
1650    GOTO 2490
1660 LET T0=T0+1                       'COUNT SINNERS CAPTURED
1670 'CHECK OUT POSSIBLE MOVES
1680 FOR I=1 TO 3
1690    FOR J=TI TO 1 STEP -1
1700       LET T9=1+INT(RND*J)         'PICK A MOVE AT RANDOM
1710       LET D$(P(T9,1),P(T9,2))=" "
1720       LET D$(P(T9,3),P(T9,4))="*"
1730       IF I<3 THEN 1790            'LOOK FOR FIENDS IN LINE
1740          PRINT "*** THE SINNERS ARE WINNERS!!"  'BELLS
1750          GOSUB 2750
1760          PRINT "*** ALL SURVIVING SINNERS ARE FREE!"  'BELLS
1770          GOTO 2500
1780          'SEEK MINIMUM ALIGNMENT OF FIENDS
1790       FOR K=1 TO 5
1800          LET R9=C9=0
1810          FOR L=1 TO 5
1820             IF D$(K,L)<>"*" THEN 1860  'CHECK ROW K FOR FIENDS
1830                LET R9=R9+1
1840                IF R9<=1 THEN 1860
1850                   GOTO 1900
1860             IF D$(L,K)<>"*" THEN 1900  'CHECK COLUMN K FOR FIENDS
1870                LET C9=C9+1
1880                IF C9<=1 THEN 1900
1890                   GOTO 1980
1900          NEXT L
1910       NEXT K
1920       GOSUB 2750                  'PRINT BOARD
1930       IF T0<22 THEN 1960          'ANY SINNERS LEFT?
1940          PRINT "*** THAT'S THE END OF THE SINNERS!!"  'BELLS
1950          GOTO 2500
1960       GOTO 2130                   'CONTINUE GAME...
1970
1980       LET D$(P(T9,1),P(T9,2))="*"  'RESET DISPLAY
1990       LET DS(P(T9,3),P(T9,4))="."
2000       FOR J1=1 TO 4                'REARRANGE POSSIBLE MOVE MATRIX
2010          LET U(J1)=P(J,J1)
2020       NEXT J1
2030       FOR J1=1 TO 4
2040          LET P(J,J1)=P(T9,J1)
2050       NEXT J1
2060       FOR J1=1 TO 4
2070          LET P(T9,J1)=U(J1)
2080       NEXT J1
2090    NEXT J
2100 NEXT I
2110
2120 'GET MOVE FROM PLAYER
2130 PRINT "SINNERS' MOVE";
2140 LINPUT A$
2150 IF LEN(A$)>10 THEN 2550           'TOO MUCH INPUT
2160    GOSUB 2610                     'PROCESS INPUT
2170    IF A(1)=83 THEN 2530           'LOOK FOR S
2180       IF A(1)=82 THEN 2480        'LOOK FOR R
2190          IF A(1)=72 THEN 2360     'LOOK FOR H
2200             IF (69-A(1))+(A(1)-65)<0 THEN 2330  'LOOK FOR A-E
2210                LET R1=A(1)-64     'R1 IS ROW OF SINNER
2220                IF (53-A(2))+(A(2)-49)<0 THEN 2330  'LOOK FOR 1-5
2230                   LET C1=A(2)-48  'C1 IS COLUMN OF SINNER
2240                   IF D$(R1,C1)<>"*" THEN 2330  'LOOK FOR SINNER
2250                      FOR J1=1 TO 4  'FIND DESIRED DIRECTION
2260                         IF A(3)<>M(J1) THEN 2320
2270                            IF D$(R1+R(J1),C1+C(J1))<>" " THEN 2310
2280                               LET D$(R1+R(J1),C1+C(J1))="."
2290                               LET DS(R1,C1)=" "
2300                               GOTO 1460  'CONTINUE GAME...
2310                            GOTO 2330
2320                      NEXT J1
2330             PRINT "ILLEGAL MOVE!"
2340             GOTO 2550
2350             'HELP SECTION PRINTS LEGAL MOVES.
2360           PRINT "THE SINNERS HAVE THESE LEGAL MOVES:"
2370           FOR J=1 TO 5
2380              FOR K=1 TO 5
2390                 IF D$(J,K)<>"." THEN 2440  'LOOK FOR SINNER
2400                    FOR L=1 TO 4    'LOOK N,E,S,W IN TURN
2410                       IF D$(J+R(L),K+C(L))<>" " THEN 2430  'LEGAL?
2420                          PRINT "  ";CHR$(J+64);CHR$(K+48);CHR$(M(L));
2430                    NEXT L
2440              NEXT K
2450           NEXT J
2460           PRINT CHR$(10)
2470           GOTO 2130
2480       PRINT "*** THE SINNERS GIVE UP!!"
2490       PRINT "*** SINK DOWN, D
                                  O
                                   W
                                    N... (SHUDDER) ...INTO THE BLACK PIT!!"
2500       PRINT
2510       PRINT "TYPE RUN TO PLAY AGAIN."
2520       STOP
2530    PRINT "PROGRAM HALTED!"
2540    STOP
2550 PRINT "INPUT IGNORED. TYPE HELP IF YOU NEED IT."
2560 PRINT "PLEASE TRY AGAIN..."
2570 GOTO 2130
2580
2590
2600 'PROCESS INPUT: CHANGE LOWERCASE TO UPPER, IGNORE COMMAS, SPACES, ETC.
2610 CHANGE A$ TO A
2620 LET A9=0
2630 FOR J=1 TO A(0)
2640    IF A(J)<96 THEN 2660
2650       LET A(J)=A(J)-32            'CHANGE LOWERCASE TO UPPER
2660    IF (89-A(J))*(A(J)-65)>=0 THEN 2690  'LOOK FOR LETTER (A-Y)
2670       IF (53-A(J))*(A(J)-49)=>0 THEN 2690  'LOOK FOR DIGIT (1-5)
2680          GOTO 2710                'ELSE IGNORE THIS CHARACTER
2690    LET A9=A9+1
2700    LET A(A9)=A(J)                 'STORE THIS CHARACTER
2710 NEXT J
2720 RETURN
2730
2740 'PRINT THE BOARD
2750 PRINT
2760 PRINT "  1 2 3 4 5"               'IDENTIFY COLUMNS
2770 FOR J=1 TO 5
2780    PRINT CHR$(J+64);              'IDENTIFY ROWS
2790    FOR K=5 TO 1 STEP-1.           'IGNORE TRAILING SPACES
2800       IF D$(J,K)=" " THEN 2820    
2810          GOTO 2840
2820    NEXT K
2830    GOTO 2870
2840    FOR L=1 TO K
2850       PRINT " ";DS(J,L);          'PRINT THE REST OF ROW
2860    NEXT L
2870    PRINT
2880 NEXT J
2890 PRINT
2900 RETURN
2910
2920 'INSTRUCTIONS
2930 PRINT
2940 PRINT "THREE OF SATAN'S FIENDS (MOVED BY THE COMPUTER) PLAY"
2950 PRINT "AGAINST A GROUP OF CONDEMNED SINNERS (MOVED BY YOU)."
2960 PRINT "IF THEY WIN, THE SURVIVING SINNERS WILL BE SET FREE."
2970 PRINT "OTHERWISE... (SHUDDER)... INTO THE BLACK PIT!!"
2980 PRINT
2990 PRINT "THE PLAYING FIELD HAS 25 SQUARES. HERE'S THE INITIAL SETUP:"
3000 GOSUB 2750
3010 PRINT "SINNERS ARE SHOWN AS DOTS (.) AND FIENDS AS STARS (+)."
3020 PRINT
3030 PRINT "A SINNER MAY MOVE INTO ANY ADJACENT EMPTY SQUARE. A FIEND"
3040 PRINT "MOVES ONLY INTO AN ADJACENT SQUARE WHICH CONTAINS A SINNER,"
3050 PRINT "WHO IS THEREBY CAPTURED AND REMOVED. THERE ARE NO DIAGONAL"
3060 PRINT "MOVES AND NO JUMPS."
3070 PRINT
3080 PRINT "THE SINNERS WIN IF THE THREE FIENDS ARE ALL IN A LINE (ALL"
3090 PRINT "IN THE SAME ROW OR COLUMN). THE SINNERS LOSE IF THE FIENDS,"
3100 PRINT "AT THEIR TURN, CANNOT CAPTURE A SINNER."
3110 PRINT
3120 PRINT "  N"
3130 PRINT "W + E"
3140 PRINT "  S"
3150 PRINT
3160 PRINT "TO MOVE A SINNER, TYPE 3 CHARACTERS: THE ROW (A LETTER) AND"
3170 PRINT "COLUMN (A DIGIT) GIVING SINNER'S CURRENT LOCATION, AND THE"
3180 PRINT "DIRECTION OF MOVE (JUST THE INITIAL LETTER)."
3190 PRINT "EXAMPLES:"
3200 PRINT "  B3E MEANS THE SINNER NOW AT H,3 WANTS TO MOVE EAST;"
3210 PRINT "  E5N MEANS THE SINNER NOW AT E,5 WANTS TO MOVE NORTH."
3220 PRINT
3230 PRINT "ALSO, TYPE S TO STOP, R TO RESIGN, H FOR HELP."
3240 PRINT
3250 RETURN
3260
3270 END